!to "mouse_relative.bin",plain

!zone code
!cpu 6502

* = $0300
                jmp    findmouse
                jmp    enablemouse
                jmp    disablemouse
                jmp    setmouse
                jmp    servemouse
                jmp    readmouse
                jmp    clearmouse
                jmp    posmouse
                jmp    clampmousex
                jmp    clampmousey
                jmp    clampmouse
                jmp    homemouse
                jmp    initmouse

findmouse       lda    #$ff
                sta    $07
                lda    #$00
                sta    $26
                lda    #$c0
                sta    $27
notfound        inc    $27
                lda    $27
                cmp    #$c7
                bcs    notfound1
                ldy    #$05
                lda    ($26),y
                cmp    #$38
                bne    notfound
                iny
                iny
                lda    ($26),y
                cmp    #$18
                bne    notfound
                iny
                iny
                iny
                iny
                lda    ($26),y
                cmp    #$01
                bne    notfound
                iny
                lda    ($26),y
                cmp    #$20
                bne    notfound
                lda    $27
                ora    #$c0
                sta    $07
                sta    mouse_entry1+2
                lda    $27
                asl
                asl
                asl
                asl
                sta    $08
notfound1       rts

enablemouse     lda    #$01
                bne    setmouse
disablemouse    lda    #$00
setmouse        ldy    #$12
mouse_entry     pha
                lda    #$00
                sta    $06
                lda    ($06),y
                sta    mouse_entry1+1
                pla
                ldx    $07
                ldy    $08
mouse_entry1    jsr    $c412 
                rts
                
servemouse      ldy    #$13
                bne    mouse_entry

readmouse       ldy    #$14
                bne    mouse_entry

clearmouse      ldy    #$15
                bne    mouse_entry

posmouse        ldy    #$16
                bne    mouse_entry

clampmousey     lda    #$01
                bne    clampmouse
clampmousex     lda    #$00
clampmouse      ldy    #$17
                clc
                bcc    mouse_entry

homemouse       ldy    #$18
                bne    mouse_entry

initmouse       ldy    #$19
                bne    mouse_entry



