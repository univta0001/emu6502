!cpu 6502	; Compatible with all Apple2's
!to "mousedrv.bin", plain

; constants
m_execute = $c080
m_status = $c081		; b0=error (0 = ok)
m_command = $c082
m_param = $c083

cmd_setmouse = $0
cmd_servemouse = $1
cmd_readmouse = $2
cmd_clearmouse = $3
cmd_posmouse = $4
cmd_clampmouse = $5
cmd_homemouse = $6
cmd_initmouse = $7

MON_IORTS = $ff58

;======================================

!zone code

*= $0000	; org $0000

; The Autoboot rom will call this.
start

; Autoboot and ProDOS look at the following few opcodes to detect mouse device
; $Cn05 = $38
; $Cn07 = $18
; $Cn0B = $01
; $Cn0C = $20
; $CnFB = $D6

                bit   MON_IORTS
                bvs   BASIC_ENTRY
                sec
                bcc   BASIC_ENTRY
                clv
                bvc   BASIC_ENTRY

; $Cn0b must be $01
                !byte $01

; $cn0c must be $20
                !byte $20

; Mouse Entry Points
; Assume X = $Cn, Y = $n0, A = input , interrupt is disabled, carry flag is cleared
; Except for ServeMouse
*= $0012	; org $cn12
                !byte <SET_MOUSE
                !byte <SERVE_MOUSE
                !byte <READ_MOUSE
                !byte <CLEAR_MOUSE
                !byte <POS_MOUSE
                !byte <CLAMP_MOUSE
                !byte <HOME_MOUSE
                !byte <INIT_MOUSE

*= $0020    ; org $cn20
BASIC_ENTRY     pha
                tya
                pha
                txa
                pha
                php
                sei
                jsr   $FF58
                tsx
                lda   $0100,X
                tax
                asl
                asl
                asl
                asl
                tay
                plp
                bvc   INENTRY
                lda   $38
                bne   PRENTRY
                txa
                eor   $39
                bne   PRENTRY
                lda   #$05
                STA   $38
                bne   INENTRY2

INENTRY         bcs   INENTRY2
PRENTRY         pla
                tax
                pla
                pla
                and   #$01
                sta   m_param,y
                lda   #cmd_setmouse
                ora   #$80
                bne   COMMAND_EXECUTE
INENTRY2        sta   $C081,Y
                pla
                lda   $0638,X
                tax
                pla
                tay
                pla
                lda   $0200,X
                rts

SET_MOUSE       cmp   #$10
                bcs   STATUS_RETURN
                sta   m_param,y
                lda   #cmd_setmouse
                beq   COMMAND_EXECUTE

SERVE_MOUSE     lda   $06
                lda   #$60
                sta   $06
                jsr   $0006
                sty   $06
                tsx
                lda   $0100,x
                tax
                asl
                asl
                asl
                asl
                tay
                lda   #cmd_servemouse
                bne   COMMAND_EXECUTE

READ_MOUSE      lda   #cmd_readmouse
                bne   COMMAND_EXECUTE

CLEAR_MOUSE     lda   #cmd_clearmouse
                bne   COMMAND_EXECUTE

POS_MOUSE       lda   #cmd_posmouse
                bne   COMMAND_EXECUTE

CLAMP_MOUSE     and   #$01
                sta   m_param,y
                lda   #cmd_clampmouse
                bne   COMMAND_EXECUTE

HOME_MOUSE      lda   #cmd_homemouse
                bne   COMMAND_EXECUTE

INIT_MOUSE      lda   #cmd_initmouse

COMMAND_EXECUTE sta   m_command,y
                lda   m_execute,y
                lda   m_status,y
                ror
STATUS_RETURN   rts

*= $00fb    ; org $cnfb
                !byte $d6
                !byte $ff
                !byte $ff
                !byte $ff
                !byte $01
